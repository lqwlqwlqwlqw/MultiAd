apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
if (enable_packed_aar.equalsIgnoreCase("true")) {
    apply plugin: 'com.kezong.fat-aar'
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    publishing {
        singleVariant("release")
    }

    repositories {
//        if (!enable_packed_aar.equalsIgnoreCase("true")) {
            flatDir {
                dirs 'libs'
            }
//        }
    }
}

dependencies {
    if (enable_packed_aar.equalsIgnoreCase("true")) {
        embed(name: 'open_ad_sdk_5.7.0.6', ext: 'aar')
    } else {
        api fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    }

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$appcompatVersion"

    api project(':core')
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release         // 表示发布 release（jitpack 都不会使用到）
                groupId = 'com.github.lqwlqwlqwlqw.MultiAd'
                artifactId = 'csj'       // 依赖库的名称（jitpack 都不会使用到）
                version = local_aar_version                //当前版本依赖库版本号，这个jitpack不会使用到，只是我们开发者自己查看
            }
        }
    }
}